plugins {
  id 'java'
  id 'idea'
  id 'checkstyle'
  id 'pmd'
  id 'com.bmuschko.clover' version "3.0.1"
  id 'com.github.spotbugs' version "4.2.2"
}

repositories {
  mavenCentral()
}
sourceCompatibility = 1.8
targetCompatibility = 1.8

//-----------------------------BASE CONFIGURATION--------------------------------------------
clover {

  testIncludes = ['**/*Test.java', '**/*Spec.groovy']
  testExcludes = ['**/Mock*.java']

  targetPercentage = '75%'

  compiler {
    encoding = 'UTF-8'
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
  }

  report {
    html = true
    pdf = true

    columns {
      coveredMethods format: 'longbar', min: '75'
      coveredStatements format: '%'
      coveredBranches format: 'raw'
      totalPercentageCovered format: '%', scope: 'package'
    }

  }
}
dependencies {
  clover 'org.openclover:clover:4.4.1'
  implementation("commons-io:commons-io:2.6")

  testImplementation('org.junit.jupiter:junit-jupiter:5.6.2')
  testRuntimeOnly('org.junit.jupiter:junit-jupiter-engine:5.6.2')
  testRuntimeOnly('org.junit.jupiter:junit-jupiter-params:5.6.2')
  testRuntimeOnly("org.junit.platform:junit-platform-launcher:1.6.2")
  testImplementation("org.junit.vintage:junit-vintage-engine:5.6.2")
  testImplementation('org.mockito:mockito-core:3.9.0')
  testImplementation("io.cucumber:cucumber-java:6.10.3")
  testImplementation("io.cucumber:cucumber-junit:6.10.3")
  testImplementation("io.cucumber:cucumber-junit-platform-engine:6.10.3")
}

test {
  systemProperty "nofs", System.getProperty("nofs")
  useJUnitPlatform {
    excludeTags 'integration-test'
  }
  testLogging {
    events "passed", "skipped", "failed"
  }
}

task integrationTest(type: Test) {
  group = "verification"
  useJUnitPlatform {
    includeTags 'integration-test'
  }
}

check.dependsOn integrationTest

checkstyle {
  toolVersion "7.6.1"
  checkstyleTest.enabled = false
}

check.dependsOn cloverGenerateReport

spotbugsMain {
  reports {
    html {
      enabled = true
      stylesheet = 'fancy-hist.xsl'
    }
  }
}

spotbugsTest {
  reports {
    html {
      enabled = true
      stylesheet = 'fancy-hist.xsl'
    }
  }
}

pmd {
  ignoreFailures = true
  pmdTest.enabled = false
  ruleSets = [
          'java-basic',
          'java-braces',
          'java-clone',
          'java-codesize',
          'java-comments',
          'java-controversial',
          'java-coupling',
          'java-design',
          'java-empty',
          'java-finalizers',
          'java-imports',
          'java-optimizations',
          'java-strictexception',
          'java-strings',
          'java-typeresolution',
          'java-unnecessary',
          'java-unusedcode'
  ]
}

tasks.withType(Pmd) {
  reports {
    xml.enabled = true
    html.enabled = true
  }
}

jar {
  manifest {
    attributes(
            'Main-Class': "com.tuanmhoang.study.tdd.Application"
    )
  }
  from {
    configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
  }
}
